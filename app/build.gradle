apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

ext{
    loadProperty = { name ->
        Properties properties = new Properties()
        InputStream is = null
        try {
            is = new FileInputStream(name)
            properties.load(is)
        } catch (Throwable tr) {
            tr.printStackTrace()
            println tr.getMessage()
        } finally {
            if (is != null) {
                try {
                    is.close()
                } catch (Throwable tr) {
                    tr.printStackTrace()
                }
            }
        }
        return properties
    }
}


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.xinlan.imclient"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            Properties properties = loadProperty("${rootProject.getProjectDir()}/sign.properties")
            storeFile file("${rootProject.getProjectDir()}/sign.jks")
            storePassword properties.get('storepassword')
            keyAlias properties.get('keyalias')
            keyPassword properties.get('keypassword')

            v2SigningEnabled true // enable v2 sign
        }

        release {
            Properties properties = loadProperty('sign.properties')
            storeFile file("${rootProject.getProjectDir()}/sign.jks")
            storePassword properties.get('storepassword')
            keyAlias properties.get('keyalias')
            keyPassword properties.get('keypassword')
            v2SigningEnabled true // enable v2 sign
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }

        release {
            shrinkResources true
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.android.support:multidex:1.0.0'

    implementation 'com.squareup.okhttp3:okhttp:3.4.1'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    api project(path: ':imsdk')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:recyclerview-v7:28.0.0'

}
repositories {
    mavenCentral()
}

